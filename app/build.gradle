plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.pepdoesthings.marvelchars"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "environments"

    productFlavors {
        production {
            // for this to work you need to set something like
            // MARVEL_APP_CLIENT_ID=blablabla
            // MARVEL_APP_PRIVATE_KEY=klshfbfskhbsdf
            // in your local or global gradle.properties
            it.buildConfigField 'String', 'MARVEL_CLIENT_ID', "\"$MARVEL_APP_CLIENT_ID\""
            it.buildConfigField 'String', 'MARVEL_PRIVATE_KEY', "\"$MARVEL_APP_PRIVATE_KEY\""
            it.buildConfigField 'String', 'BASE_URL', '"https://gateway.marvel.com:443"'
        }
    }

    def appName
    if (project.hasProperty("applicationName")) {
        appName = applicationName
    } else {
        appName = parent.name
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = appName + "_" + versionName + "(" + versionCode + ")-" + variant.flavorName + ".apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'

    // Fragment
    implementation "androidx.fragment:fragment-ktx:1.4.0"

    // Activity
    implementation "androidx.activity:activity-ktx:1.4.0"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.6.0'
    api 'com.squareup.okhttp3:okhttp:4.6.0'

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    //dagger hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}